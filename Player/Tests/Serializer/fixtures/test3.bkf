#!blackfire-player

group http_cache_check
    visit url(url)
        name "Loading " ~ url
        blackfire false
        expect status_code() == 200
        wait 200

    reload
        name "Checking " ~ url
        blackfire false
        expect status_code() == 200
        expect header("Cache-Control") matches "/max\-age=\\d+/"
        expect header("X-Sensiocloud-Cache") matches "/HIT/"

scenario
    name "HTTP Cache"
    set env "dev"
    set urls [ \
        "/",  \
        "/development", \
        "/license", \
        "/doc/1.x/", \
        "/doc/2.x/", \
        "/doc/2.x/filters/first.html", \
        "/api/2.x/index.html", \
    ]

    when "prod" == env
        with url in urls
            include http_cache_check

scenario
    name "Documentation"

    visit "/"
        expect status_code() == 200
        expect body() matches "/Twig is a modern template engine for PHP/"

    visit "/doc/2.x/api.html"
        expect status_code() == 200
        expect body() matches "/Twig for Developers/"

    visit "/doc/2.x/intro.html"
        expect status_code() == 200

    click link("Edit")
        expect status_code() == 302
        expect header("Location") matches "{https://github.com/login}"
        blackfire false

scenario
    name "Documentation Redirects"

    visit "/doc/2.x/api.html"
        expect status_code() == 200
        expect 1 == link("1.x").count()

    click link("1.x")
        expect status_code() == 200
        expect 1 == link("2.x").count()

    visit "/doc/tags/embed.html"
        expect status_code() == 302
        expect header("Location") == "/doc/3.x/tags/embed.html"

    visit "/for"
        expect status_code() == 302
        expect header("Location") == "/doc/3.x/tags/for.html"

    visit "/fir"
        expect status_code() == 200
        expect body() matches "/Sorry, but there is no Twig tag, filter, test, or function name/"
        expect css("p > a:contains('for')")

    visit "/else"
        expect status_code() == 302
        expect header("Location") == "/doc/3.x/tags/if.html"

    visit "/in"
        expect status_code() == 302
        expect header("Location") == "/doc/3.x/templates.html#containment-operator"

    visit "/block"
        expect status_code() == 200
        expect body() matches "/can be different things in Twig/"

scenario
    name "404 page"
    set env "dev"

    when "prod" != env
        visit "/_errors/404.html"
            expect status_code() == 200
            expect body() matches "/Sorry, the page you are looking for could not be found./"

    when "prod" == env
        visit "/foo/bar/baz"
            expect status_code() == 404
            expect body() matches "/Sorry, the page you are looking for could not be found./"

scenario
    name "Shortcuts"

    visit "/for"
        expect status_code() == 302
        expect header("Location") == "/doc/3.x/tags/for.html"